PRO MINUS_QOCA_SITES_FILES_LLH,FILES,OFILE,_EXTRA=_EX

  IF N_PARAMS() LT 2 THEN BEGIN
    FILEA=DIALOG_PICKFILE(/READ, $
      FILTER=[['*.list'],['QOCA Site List File (*.list)']], $
      TITLE='First QOCA Sites List File:')
    IF FILEA EQ '' THEN RETURN
    CD, GETPATHNAME(FILEA)
    
    FILEB=DIALOG_PICKFILE(/READ, $
      FILTER=[['*.list'],['QOCA Site List File (*.list)']], $
      TITLE='First QOCA Sites List File:')
    IF FILEB EQ '' THEN RETURN
    CD, GETPATHNAME(FILEB)
    
    OFILE=DIALOG_PICKFILE(/WRITE, $
      FILTER=[['*.list'],['QOCA Site List File (*.list)']], $
      TITLE='Output QOCA Sites List File:')
    IF OFILE EQ '' THEN RETURN
    CD, GETPATHNAME(OFILE)
    
    FILES = [FILEA,FILEB]
  ENDIF
  
  IF N_ELEMENTS(FILES) LE 0 THEN RETURN
  
  ;READ IN THE SOURCE FILE
  LINES=READ_TXT(FILES[0])
  NSITE=FIX(LINES[0])
  SITES=''
  FOR I=1,NSITE DO BEGIN
    SITES=[SITES,STRLOWCASE(STRMID(LINES[I],0,4))]
  ENDFOR
  IF N_ELEMENTS(SITES) LE 1 THEN BEGIN
    PRINT,'No sites found in '+FILES[0]
    RETURN
  ENDIF
  SITES=SITES[1:*]
  INDS=SORT(SITES)
  SITES=SITES[INDS]
  IND=UNIQ(SITES)
  INDS=INDS[IND]
  SITES=SITES[IND]
  
  ;READ IN THE MINUS FILE
  LINESM=READ_TXT(FILES[1])
  NSITEM=FIX(LINESM[0])
  SITESM=''
  FOR I=1,NSITEM DO BEGIN
    SITESM=[SITESM,STRLOWCASE(STRMID(LINESM[I],0,4))]
  ENDFOR
  IF N_ELEMENTS(SITESM) LE 1 THEN BEGIN
    PRINT,'No sites found in '+FILES[1]
  ENDIF ELSE BEGIN
    SITESM=SITESM[1:*]
    SITESM=SITESM[SORT(SITESM)]
    SITESM=SITESM[UNIQ(SITESM)]
  ENDELSE
  
  ;MINUS
  IF SITESM[0] NE '' THEN BEGIN
    INDSR=-1
    FOR I=0,N_ELEMENTS(SITES)-1 DO BEGIN
      POS=WHERE(SITES[I] EQ SITESM)
      IF POS[0] NE -1 THEN CONTINUE
      INDSR=[INDSR,INDS[I]]
    ENDFOR
    IF N_ELEMENTS(INDSR) LE 1 THEN BEGIN
      INDS=-1
    ENDIF ELSE BEGIN
      INDSR=INDSR[1:*]
      INDS=INDSR
    ENDELSE
  ENDIF
  
  IF INDS[0] EQ -1 THEN BEGIN
    PRINT,'No sites remain'
    RETURN
  ENDIF
  
  ;OUTPUT THE RESULT
  OPENW,FID,OFILE,/GET_LUN
  PRINTF,FID,N_ELEMENTS(INDS),FORMAT='(I5)'
  FOR LI=0,N_ELEMENTS(INDS)-1 DO BEGIN
    PRINTF,FID,LINES[INDS[LI]+1],FORMAT='(A)'
  ENDFOR
  FREE_LUN,FID
  
END

